apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'
apply plugin: rootProject.libs.plugins.dokka.get().pluginId

task sourceJar(type: Jar) {
    getArchiveClassifier().set('sources')
    from sourceSets.main.java.srcDirs
}

tasks.withType(dokkaHtml.getClass()).configureEach {
    pluginsMapConfiguration.set(
            ["org.jetbrains.dokka.base.DokkaBase": """{ "separateInheritedMembers": true}"""]
    )
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    getArchiveClassifier().set('javadoc')
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives sourceJar
    archives javadocJar
}

afterEvaluate {

    publishing {
        publications {
            release(MavenPublication) {
                groupId = rootProject.ext.artifactoryGroup
                artifactId = project.name
                version = rootProject.ext.versionName
                pom {
                    name = "StateGenie"
                    description = "Generate extensive sealed interface"
                    url = "https://github.com/ArindomGhosh/StateGenie"
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = "ArindomGhosh"
                            name = "Arindom Ghosh"
                            email = "arindomghosh23@gmail.com"
                        }
                    }
                    scm {
                        connection = "scm:git:github.com/ArindomGhosh/StateGenie.git"
                        developerConnection = "scm:git:ssh://github.com/ArindomGhosh/StateGenie.git"
                        url = "https://github.com/ArindomGhosh/StateGenie/tree/main"
                    }
                }
                from components.java
                artifact(sourceJar)
                artifact(javadocJar)
            }
        }
    }

    artifactory {
        contextUrl = artifactoryUrl
        //The base Artifactory URL if not overridden by the publisher/resolver

        publish {
            repository {
                repoKey = repositoryKey
                username = artifactoryUserName
                password = artifactoryPassword
                maven = true

            }
            defaults {
                publications('release')
                publishArtifacts = true
                publishPom = true
            }
        }
    }
}